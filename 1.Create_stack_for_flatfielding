//Create a stack to be used to create a normalized image
	
//Select all of the relevant directories 
   
   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select an output directory");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

//Open all of the files within subfolders 
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  //Order of acquisition
  //C1 - 405, C2 - 488, C3 - 561, C4 - 647, C5 - BF
  
  function processFile(path) {
       if (endsWith(path, ".tif")) {
       		//Open the image with .tif extension
       		open(path);
       		
       		//Get the file name
       		name = getTitle();
       		
       		//Close all channels other than the one you would like to create a stack for
       		run("Split Channels");
       		selectWindow("C1-" + name); 
       		close();
       	   	selectWindow("C2-" + name); 
       		close();
       		selectWindow("C3-" + name); 
       		close();
       		selectWindow("C5-" + name); 
       		close();
       }
  }

//Create stack 
run("Images to Stack");

//Save stack 
outName1 = "Stack";
saveAs("tif", outputDir + "/" + outName1);

//Close everything
run("Close All");
  
			
			
       		
