//This macro flatfields all the channels of a multi-channel image acquisition 

//Select all of the relevant directories 
   
   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select an output directory");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

//Open all of the files within subfolders 
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
       if (endsWith(path, ".tif")) {
       		//Open the image with .tif extension
       		open(path);
       		
       		//Get the file name
       		imagesName=getTitle();
       		
       		//Duplicate the original image and rename
       		run("Duplicate...", "duplicate");
       		
       		//Get image details 
       		getDimensions(width, height, channels, slices, frames);
       		
       		//C1 - 405 B220, C2 - pSyk 488, C3 - Ag 550, C4 - Ag internalised 647, C5 - BF 
       		if (channels == 5) {
       			//Set the image properties 
	       		run("Properties...", "channels=channels slices=slices frames=frames unit=Âµm pixel_width=0.11 pixel_height=0.11 voxel_depth=1.0000000");

	       		//Rename the file 
	       		rename("Stack");
	       		
	       		//Subtract camera counts
	       		open("//rds.er.kcl.ac.uk/prj_ph_b_cell_apc_viscosity/Data_and_Analysis Oct2022-Sep2023/30.March_2023/20231403_Calcium_and_piezo_inhibitors_NIP3/Raw_data/No_laser/AVG_Stack.tif");
			imageCalculator("Subtract create stack", "Stack","AVG_Stack.tif");
				
			//Rename the background subtracted image
			rename("Image");
				
			//Split the channels 
			run("Split Channels");
       		    
       		   	//Flatfield the B220 channel 
			selectWindow("C1-Image");
			img1 = getTitle(); 
			open("//rds.er.kcl.ac.uk/prj_ph_b_cell_apc_viscosity/Data_and_Analysis Oct2022-Sep2023/30.March_2023/20231403_Calcium_and_piezo_inhibitors_NIP3/Analysis/TIRF_signalling/1.Flatfields/405_flatfield/Normalised_Stack.tif");
			//Crop to be 600x600 pixels to remove uneven illumination at the edges
			//Adjust for different microscopes
			makeRectangle(300, 300, 600, 600);
	       		run("Crop");
			img2 = getTitle();
			imageCalculator("Divide create stack", img1, img2);
			//Rename so easier to merge at the end 
			selectWindow("Result of C1-Image");
			rename("B220");
				
			//Flatfield the pSyk channel as for B220 channel
			selectWindow("C2-Image");
			img1 = getTitle(); 
			open("//rds.er.kcl.ac.uk/prj_ph_b_cell_apc_viscosity/Data_and_Analysis Oct2022-Sep2023/30.March_2023/20231403_Calcium_and_piezo_inhibitors_NIP3/Analysis/TIRF_signalling/1.Flatfields/488_flatfield/Normalised_Stack.tif");
			makeRectangle(300, 300, 600, 600);
	       		run("Crop");
			img2 = getTitle();
			imageCalculator("Divide create stack", img1, img2);
			selectWindow("Result of C2-Image");
			rename("pSyk");
				
			//Flatfield the antigen channel as for B220 channel 
			selectWindow("C3-Image");
			img1 = getTitle(); 
			open("//rds.er.kcl.ac.uk/prj_ph_b_cell_apc_viscosity/Data_and_Analysis Oct2022-Sep2023/30.March_2023/20231403_Calcium_and_piezo_inhibitors_NIP3/Analysis/TIRF_signalling/1.Flatfields/561_flatfield/Normalised_Stack.tif");
			makeRectangle(300, 300, 600, 600);
	       		run("Crop");
			img2 = getTitle();
			imageCalculator("Divide create stack", img1, img2);
			selectWindow("Result of C3-Image");
			rename("Ag");

			//Flatfield the internalised antigen channel as for B220 channel
			selectWindow("C4-Image");
			img1 = getTitle(); 
			open("//rds.er.kcl.ac.uk/prj_ph_b_cell_apc_viscosity/Data_and_Analysis Oct2022-Sep2023/30.March_2023/20231403_Calcium_and_piezo_inhibitors_NIP3/Analysis/TIRF_signalling/1.Flatfields/647_flatfield/Normalised_Stack.tif");
			makeRectangle(300, 300, 600, 600);
			run("Crop");
			img2 = getTitle();
			imageCalculator("Divide create stack", img1, img2);
			selectWindow("Result of C4-Image");
			rename("Int_Ag");

			//Rename the BF channel
			selectWindow("C5-Image");
			rename("BF");

			//Merge channels and save in new order
			run("Merge Channels...", "c1=Ag c2=Int_Ag c3=pSyk c4=B220 c5=BF create");
			rename("Merge"); 
			
			//Change lookup tables for each channel
			run("Make Composite");
			Stack.setDisplayMode("color");
			Stack.setChannel(1);
			run("Magenta");
			resetMinAndMax();
			Stack.setChannel(2);
			run("Cyan");
			resetMinAndMax();
			Stack.setChannel(3);
			run("Green");
			resetMinAndMax();
			Stack.setChannel(4);
			run("Yellow");
			resetMinAndMax();
			Stack.setChannel(5);
			run("Grays");
			resetMinAndMax();

			//Save the new stack 
			outName1 = "Flatefielded_" + imagesName;
			saveAs("tif", outputDir + "/" + outName1);

			//Close everything
			run("Close All");
       		}
        }
  }
