//This macro changes the image type to composite

//Select all of the relevant directories 
   
   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select an output directory");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

//Open all of the files within subfolders 
   
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
       if (endsWith(path, ".tif")) {
       		//Open the image
       		open(path);
       		
       		//Get the file name
       		imagesName=getTitle();
       		
       		//Duplicate the original image 
       		run("Duplicate...", "duplicate");
       		
       		//Make composite
       		run("Make Composite");
	
		//Save stack 
		outName1 = imagesName;
		saveAs("tif", outputDir + "/" + outName1);

		//Close everything
		run("Close All");
       		}
        }
